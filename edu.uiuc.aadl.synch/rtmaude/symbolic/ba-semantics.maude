
mod BEHAVIOR-ACTION-CONF is
  including VAR-GEN .
  including BEHAVIOR-CONF .

  op vargen : VarGen -> BehaviorConfItem [ctor format (b! o)] .
  op const : BoolExp -> BehaviorConfItem [ctor format (b! o)] .

  vars CONST CONST' : BoolExp .
  var BCF : BehaviorConf .

  op addConst : BehaviorConf BoolExp ~> BehaviorConf .
  eq addConst(const(CONST) BCF, CONST') = const(CONST and CONST') BCF .
endm


--- A behavior action may change data subcomponents or output features.
--- Currently, we do not assume predefined functions like sin, cos, etc.
mod BEHAVIOR-ACTION-SEMANTICS is
  including BEHAVIOR-ACTION-SYNTAX .
  including BEHAVIOR-ACTION-CONF .
  including BEHAVIOR-EXPRESSION-SEMANTICS .

  var N : Nat .
  var E : Exp .
  vars B : BoolExp .
  var VAL : VarValuation .  
  var FMAP : FeatureMap . 
  var DATA : DataValuation .
  vars PROPS : PropertyAssociation .  
  var BCF BCF' : BehaviorConf .  
  var CR : ComponentRef .  
  var PI : FeatureId .  
  var VI : VarId .
  var A : Action .  
  vars AS AS' AS'' : ActionGroup .
  var ASQ : ActionSequence .  
  var AST : ActionSet .
  vars AE AE' : AADLExp .  
  var ELSIFS : ElseIfs .
  var D : DataContent . 
  var FC : FeatureMapContent . 

  op lbound : Nat -> BehaviorConfItem [ctor format (b! o)] .

--- execAction(actions, local variables | features | data | properties | loop bound | constraints)
  op execAction : Action BehaviorConf ~> BehaviorConf [format (m! o)] .

--- action blocks/sets/sequences

  eq execAction({A ; ASQ}, BCF) = execAction({ASQ}, execAction(A, BCF)) .

  --- allows nondeterministic choices
  rl execAction({A & AST}, BCF) => execAction({AST}, execAction(A, BCF)) .  

  eq execAction({A}, BCF) = execAction(A, BCF) .

  --- empty action
  eq execAction({skip}, BCF) = BCF . 

--- Assignment: local variables, output ports/params, and data (sub)components
--- An assignment can be executed only if the value to be assigned is defined.
--- Note that true will be added for each flag B, while B added to CONST.

  eq execAction(v{VI} := AE, BCF) =  execVarAssign(VI, eval(AE,BCF), BCF) .
  eq execAction(f{PI} := AE, BCF) = execPortAssign(PI, eval(AE,BCF), BCF) .
  eq execAction(c{CR} := AE, BCF) = execDataAssign(CR, eval(AE,BCF), BCF) .

  op execVarAssign : VarId DataContent BehaviorConf ~> BehaviorConf [format (m! o)] .
  eq execVarAssign(VI, E # B, local(VAL) BCF)
   = local(insert(VI, E # bool(true), VAL)) addConst(BCF, B) .

  --- output data ports (NOTE: not checked whether PI is indeed an output port)
  op execPortAssign : FeatureId DataContent BehaviorConf ~> BehaviorConf [format (m! o)] .
  eq execPortAssign(PI, E # B, feature(FMAP) BCF)
   = feature(insert(PI, E # bool(true), FMAP)) addConst(BCF, B) .
  
 --- only basic values.
  op execDataAssign : ComponentRef DataContent BehaviorConf ~> BehaviorConf [format (m! o)] .
  eq execDataAssign(CR, E # B, data(DATA) BCF)
   = data(insert(CR, E # bool(true), DATA)) addConst(BCF, B) .
 
  op target : VarAADLExp ~> AssignmentTarget [format (m! o)] .
  eq target(v[VI]) = v{VI} .
  eq target(f[PI]) = f{PI} .
  eq target(c[CR]) = c{CR} .

--- event/event data port for the environment
  eq execAction(PI !, BCF) = execPortAssign(PI, * # bool(true), BCF) .

  eq execAction(PI !(AE), BCF) = execPortAssign(PI, eval(AE,BCF), BCF) .


--- Branch actions: currently, all possible paths are explicitly enumerated
--- A branch can be executed only if the condition value is defined.

  --- if-then
  rl execAction(if (AE) AS end if, BCF)
  => execAction(AS, addConst(BCF, branchCD(eval(AE, BCF)))) .
    
  rl execAction(if (AE) AS end if, BCF)
  => addConst(BCF, branchCD(eval(not(AE), BCF))) .
    
  --- if-then-else
  rl execAction(if (AE) AS else AS' end if, BCF)
  => execAction(AS, addConst(BCF, branchCD(eval(AE, BCF)))) . 

  rl execAction(if (AE) AS else AS' end if, BCF)
  => execAction(AS', addConst(BCF, branchCD(eval(not(AE), BCF)))) .

  --- elsif (single)
  rl execAction(if (AE) AS (elsif (AE') AS') end if, BCF)
  => execAction(AS, addConst(BCF, branchCD(eval(AE, BCF)))) .

  rl execAction(if (AE) AS (elsif (AE') AS') end if, BCF)
  => execAction(if (AE') AS' end if, addConst(BCF, branchCD(eval(not(AE), BCF)))) .

  --- elsif (many)
  rl execAction(if (AE) AS ((elsif (AE') AS') ELSIFS) end if, BCF)
  => execAction(AS, addConst(BCF, branchCD(eval(AE, BCF)))) .

  rl execAction(if (AE) AS ((elsif (AE') AS') ELSIFS) end if, BCF)
  => execAction(if (AE') AS' ELSIFS end if, addConst(BCF, branchCD(eval(not(AE), BCF)))) .

  --- if-elsif-else
  rl execAction(if (AE) AS (elsif (AE') AS') else AS'' end if, BCF)
  => execAction(AS, addConst(BCF, branchCD(eval(AE, BCF)))) .

  rl execAction(if (AE) AS (elsif (AE') AS') else AS'' end if, BCF)
  => execAction(if (AE') AS' else AS'' end if, addConst(BCF, branchCD(eval(not(AE), BCF)))) .

  --- if-elsif(many)-else
  rl execAction(if (AE) AS ((elsif (AE') AS') ELSIFS) else AS'' end if, BCF)
  => execAction(AS, addConst(BCF, branchCD(eval(AE, BCF)))) .

  rl execAction(if (AE) AS ((elsif (AE') AS') ELSIFS) else AS'' end if, BCF)
  => execAction(if (AE') AS' ELSIFS else AS'' end if, addConst(BCF, branchCD(eval(not(AE), BCF)))) .

--- loop action. NOTE: The for loop is not supported yet.
  rl execAction(while (AE) {AS}, lbound(s(N)) BCF)
  => execAction({AS ; while (AE) {AS}}, lbound(N) addConst(BCF, branchCD(eval(AE, BCF)))) .

  rl execAction(while (AE) {AS}, BCF)
  => addConst(BCF, branchCD(eval(not(AE), BCF))) .

  eq execAction(do AS until (AE), BCF)
   = execAction(while (not(AE)) {AS}, execAction({AS}, BCF)) .
endm


mod BEHAVIOR-TRANSITION-RESULT is
  including BEHAVIOR-EXPRESSION-SEMANTICS .
  including BEHAVIOR-LOCATION-ID .
  including BEHAVIOR-ACTION-CONF .
  protecting CHECKSAT .
  
  sort BATransResult .
  op _|_|_|_|_ : Location FeatureMap DataValuation BoolExp VarGen -> BATransResult [ctor] .

  var L : Location .
  var FMAP : FeatureMap .
  var DATA : DataValuation .
  var CONST : BoolExp .
  var BCF : BehaviorConf .
  var GEN : VarGen .

  op transResult : Location BehaviorConf ~> BATransResult .
 ceq transResult(L, feature(FMAP) data(DATA) const(CONST) vargen(GEN) BCF) 
   = L | FMAP | DATA | CONST | GEN
  if check-sat(CONST) .
endm


view BATransResult from TRIV to BEHAVIOR-TRANSITION-RESULT is
  sort Elt to BATransResult .
endv


--- NOTE: we do not support a transition priority yet.
mod BEHAVIOR-TRANSITION-SEMANTICS is
  including BEHAVIOR-ACTION-SEMANTICS .
  including BEHAVIOR-TRANSITION-RESULT .
  including BEHAVIOR-SYMBOLIC-LOCATION .
  including BEHAVIOR-TRANSITION-AUX .

  vars SL L L' : Location . 
  var LS : Set{Location} .
  vars B B' : BoolExp .
  vars VAL VAL' : VarValuation .  
  vars BCF : BehaviorConf .
  vars TRS TRS' : Set{Transition} .  
  var GUARD : TransGuard .  
  var ACT : ActionBlock .
  var N : Nat .

  op location : Location -> BehaviorConfItem [ctor format (b! o)] .
  op complete : Set{Location} -> BehaviorConfItem [ctor format (b! o)] .
  op trans : Set{Transition} -> BehaviorConfItem [ctor format (b! o)] .
  op tbound : Nat -> BehaviorConfItem [ctor format (b! o)] .

--- Execute (nondeterministic) transitions until reaching a complete state,
  op execTrans : BehaviorConf ~> BATransResult [format (m! o)] .

--- NOTE: SL can be potentially a symbolic location
  crl [trans]:
     execTrans(location(SL) trans(TRS) tbound(s(N)) local(VAL) BCF)
   =>
     execTStep(VAL, addConst(location(L') trans(TRS) tbound(N) execAction(ACT, local(VAL) BCF), B and B'))
   if (L -[GUARD]-> L' ACT) ; TRS' := TRS
   /\ B  := locConst(SL, L) 
   /\ B' := guardConst(GUARD, outTrans(L, TRS'), local(VAL) BCF) 
   /\ check-sat(B) . --- should be checked to avoid extra computation


  op execTStep : VarValuation BehaviorConf ~> BATransResult [format (m! o)] .
   eq execTStep(VAL, location(L) complete(LS) local(VAL') BCF) 
    = if L in LS
      then transResult(L, BCF) 
      else execTrans(location(L) complete(LS) local(VAL) BCF) fi .
endm

