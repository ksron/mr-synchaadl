
mod ENV-TRANSITION-SEMANTICS is
  including ENV-CONTINUOUS-SEMANTICS .
  including BEHAVIOR-SYMBOLIC-LOCATION .
  including BEHAVIOR-TRANSITION-RESULT .
  protecting DEFAULT-PROPERTIES .
  protecting HYBRID-SYNCHAADL-PROPERTIES .
  including ENV-TRANSITION-AUX .
  protecting CHECKSAT .
  protecting VAR-GEN .

  vars T T' : RealExp .
  vars SL L L' : Location .
  var LT UT : Rat .
  vars SIT SIT' : Set{InterTiming} .
  var JUMPS : Set{EnvJump} .
  var FMAP : FeatureMap .
  var FLOW : FlowItem .
  var FLOWS : Set{EnvFlow} .
  var E : Exp .
  var D : DataContent .
  vars SK PER : RealExp .
  vars CONST B B' B'' : BoolExp .
  var PI : FeatureId .
  var CI : ComponentId .
  var CR : ComponentRef .
  vars GEN GEN' : VarGen .
  vars ECF ECF' ECF'' : BehaviorConf .
  vars DATA DATA' : DataValuation .
  vars PORTS PORTS' : Configuration .
  var CONXS : Set{Connection} .
  var PROPS : PropertyAssociation .
  var ATTS : AttributeSet .

  op time : RealExp -> BehaviorConfItem [ctor format (b! o)] .

  --- run continous step up to time T', and then a discrete transition
  op execEnv  : BehaviorConf ~> BATransResult [format (m! o)] .

 crl [env-cont]: 
     execEnv(time(T)  mode(SL) vargen(GEN)  prop(PROPS) ECF) 
  => envStep(time(T') mode(L)  vargen(GEN') prop(PROPS) addConst(updateEnvData(DATA,ECF), B))
  if flows((L FLOW) ; FLOWS) ECF'' := ECF
  /\ {T',GEN'} := gen(GEN,Real)
  /\ PER # B'' := eval(p[TimingProperties::Period], prop(PROPS))
  /\ B' := locConst(SL,L) 
  /\ check-sat(B')  --- should be checked to avoid extra computation
  /\ B  := (T <= T') and (T' <= PER) and B' and B''
  /\ DATA := execFlow(FLOW, T' - T, ECF) .


  --- envStep(time, mode, sampling, response, env conf, env component)
  --- performs a discrete transition (either sampling or response)

  op envStep : BehaviorConf ~> BATransResult [format (m! o)] .

 crl [env-samp]: 
     envStep(time(T) envcon(CONXS,PORTS) vargen(GEN) prop(PROPS) sampling((CR : (LT,UT), SIT)) ECF)
  => execEnv(time(T) envcon(CONXS,PORTS) vargen(GEN) prop(PROPS) sampling(SIT) 
     addConst(updateEnvFeature(CONXS,PORTS,CR,ECF), B))
  if SK # B' := eval(p[HybridSynchAADL::MaxClockDeviation], prop(PROPS)) 
  /\ B := (real(LT) <= T) and (T <= real(UT) + real(2) * SK) and B' .

  --- NOTE: we assume that every sample time is *strictly less* than any response time.

   --- a command has arrived at port PI from controller CR
 crl [env-resp-move]: 
     envStep(time(T) mode(L)  envcon(CONXS,PORTS) vargen(GEN) prop(PROPS) response((CR : (LT,UT), SIT)) ECF)
  => execEnv(time(T) mode(L') envcon(CONXS,PORTS) vargen(GEN) prop(PROPS) response(SIT) addConst(ECF', B))
  if jumps((L -[ PI ]-> L') ; JUMPS) sampling(empty) feature(FMAP) ECF'' := ECF
  /\ SK # B'' := eval(p[HybridSynchAADL::MaxClockDeviation], prop(PROPS)) 
  /\ B' := isPortPresent(PI, FMAP)
  /\ < PI : C:EnvInPort | target : CR, ATTS > PORTS' := PORTS
  /\ B := (real(LT) <= T) and (T <= real(UT) + real(2) * SK) and B' and B'' 
  /\ ECF' := updateResponseData(PI, FMAP[PI], CONXS, ECF) .

   --- when no command has arrived from controller CR
 crl [env-resp-stay]: 
     envStep(time(T) mode(L) envcon(CONXS,PORTS) vargen(GEN) prop(PROPS) response((CR : (LT,UT), SIT)) ECF)
  => envStep(time(T) mode(L) envcon(CONXS,PORTS) vargen(GEN) prop(PROPS) response(SIT) addConst(ECF, B))
  if jumps(JUMPS) sampling(empty) feature(FMAP) ECF'' := ECF
  /\ B := allPortsAbsent(L, JUMPS, FMAP, CR, PORTS) .
 

 ceq envStep(time(T) mode(L) sampling(empty) response(empty) prop(PROPS) ECF)
   = transResult(L, prop(PROPS) addConst(ECF, B and (T === PER)))
  if PER # B := eval(p[TimingProperties::Period], prop(PROPS)) .


  ---  update a conf from DATA at once
  op updateEnvData : DataValuation BehaviorConf ~> BehaviorConf .
  eq updateEnvData((CR |=> E # B, DATA), data(DATA') ECF)
   = updateEnvData(DATA, data(insert(CR, E # bool(true), DATA')) addConst(ECF, B)) .
  eq updateEnvData(empty, ECF) = ECF .

  --- update the data according to the data value at PI and the connection.
  op updateResponseData : FeatureId FeatureMapContent Set{Connection} BehaviorConf ~> BehaviorConf [format (m! o)] .
  eq updateResponseData(PI, D : B, (PI =>> CI) ; CONXS, data(DATA) ECF)
   = data(insert(CI, D, DATA)) addConst(ECF, B) .
  eq updateResponseData(PI, D : B, CONXS, ECF) = ECF [owise] . --- if no connection from PI exists
endm

