
mod ENV-TRANSITION-EXEC is
  including ENV-CONTINUOUS-SEMANTICS .
  including BEHAVIOR-SYMBOLIC-LOCATION .
  including BEHAVIOR-TRANSITION-RESULT .
  protecting DEFAULT-PROPERTIES .
  protecting HYBRID-SYNCHAADL-PROPERTIES .
  including ENV-TRANSITION-AUX .
  protecting CHECKSAT .
  protecting VAR-GEN .

  vars T T' : RealExp .
  vars SL L L' : Location .
  var LT UT : Rat .
  vars SIT SIT' : Set{InterTiming} .
  var JUMPS : Set{EnvJump} .
  var FMAP : FeatureMap .
  var FLOW : FlowItem .
  var FLOWS : Set{EnvFlow} .
  vars E E' : Exp .
  var D : DataContent .
  vars SK PER : RealExp .
  vars CONST B B' B'' : BoolExp .
  var PI : FeatureId .
  var PRS : Set{FeatureRef} .
  var CI : ComponentId .
  var CR : ComponentRef .
  vars GEN GEN' : VarGen .
  vars ECF ECF' ECF'' : BehaviorConf .
  vars DATA DATA' : DataValuation .
  vars PORTS PORTS' : Configuration .
  var CONXS : Set{Connection} .
  var PROPS : PropertyAssociation .
  var ATTS : AttributeSet .

  op time : RealExp -> BehaviorConfItem [ctor format (b! o)] .

  --- run continous step up to time T', and then a discrete transition
  op execEnvTrans  : BehaviorConf ~> BATransResult [format (m! o)] .

 crl [env-cont]: 
     execEnvTrans(time(T)  mode(SL) vargen(GEN)  ECF) 
  => envStep(time(T') mode(L)  vargen(GEN') addConst(ECF', T <= T' and B))
  if flows((L FLOW) ; FLOWS) ECF'' := ECF
  /\ B := locConst(SL,L) 
  /\ check-sat(B)  --- should be checked to avoid extra computation
  /\ {T',GEN'} := gen(GEN,Real)
  /\ DATA := execFlow(FLOW, T' - T, ECF)
  /\ ECF' := updateEnvData(DATA,ECF) .


  --- envStep(time, mode, sampling, response, env conf, env component)
  --- performs a discrete transition (either sampling or response)

  op envStep : BehaviorConf ~> BATransResult [format (m! o)] .

 crl [env-samp]: 
     envStep(time(T) sampling((CR : (LT,UT), SIT)) ECF)
  => execEnvTrans(time(T) sampling(SIT) addConst(ECF', B))
  if B := timeConst(T, LT, UT, ECF) 
  /\ ECF' := updateEnvFeature(CR,ECF) .

  --- NOTE: we assume that each sample time is *strictly less* than the corresponding response time.

   --- a command has arrived at port PI from controller CR, together with other data ports connected to CR
 crl [env-resp-move]: 
     envStep(time(T) mode(L)  response((CR : (LT,UT), SIT)) ECF)
  => execEnvTrans(time(T) mode(L') response(SIT) addConst(ECF', B))
  if jumps((L -[ PI, PRS ]-> L') ; JUMPS) ECF'' := ECF
  /\ validTarget(PI, CR, ECF) 
  /\ B' := isPortPresent(PI, ECF)
  /\ B := timeConst(T, LT, UT, ECF) and B' 
  /\ ECF' := updateResponseData(CR, ECF) .

   --- when no (event/event data) command has arrived from controller CR 
   --- No mode change, but DATA are updated by data input from CR
 crl [env-resp-stay]: 
     envStep(time(T) mode(L) response((CR : (LT,UT), SIT)) ECF)
  => envStep(time(T) mode(L) response(SIT) addConst(ECF', B'))
  if B' := allPortsAbsent(L, CR, ECF) 
  /\ ECF' := updateResponseData(CR, ECF) .
 

 ceq envStep(time(T) mode(L) sampling(empty) response(empty) prop(PROPS) ECF)
   = transResult(L, prop(PROPS) addConst(ECF, B and (T === PER)))
  if PER # B := eval(p[TimingProperties::Period], prop(PROPS)) .

---
  op timeConst : RealExp Rat Rat BehaviorConf ~> BoolExp .
  ceq timeConst(T, LT, UT, prop(PROPS) ECF)
    = (real(LT) <= T) and (T <= real(UT) + real(2) * SK) and B
   if SK # B := eval(p[HybridSynchAADL::MaxClockDeviation], prop(PROPS)) .

  ---  update a conf from DATA at once
  op updateEnvData : DataValuation BehaviorConf ~> BehaviorConf .
  eq updateEnvData((CR |=> E # B, DATA), data(DATA') ECF)
   = updateEnvData(DATA, data(insert(CR, E # bool(true), DATA')) addConst(ECF, B)) .
  eq updateEnvData(empty, ECF) = ECF .

  --- update the data from input ports connected to CR; for absent data ports,
  --- previous values are used (i.e. no change on data components)
  op updateResponseData : ComponentRef BehaviorConf ~> BehaviorConf [format (m! o)] .
  op updateResponseData : Set{Connection} ComponentRef FeatureMap BehaviorConf ~> BehaviorConf [format (m! o)] .

  eq updateResponseData(CR, envcon(CONXS,PORTS) feature(FMAP) ECF)
   = updateResponseData(CONXS, CR, FMAP, envcon(CONXS,PORTS) feature(FMAP) ECF) .
  ceq updateResponseData((PI =>> CI) ; CONXS, CR, FMAP, data(DATA) ECF)
    = updateResponseData(CONXS, CR, FMAP, data(DATA') ECF') 
   if validTarget(PI, CR, ECF)
   /\ E  # B : B'' := FMAP[PI]   --- NOTE: B'' (freshness) is always true
   /\ E' # B' := DATA[CI] 
   /\ DATA' := insert(CI, (B ? E : E') # (B or B'), DATA)
   /\ ECF'  := addConst(ECF, B'' and (B or B')) .
  eq updateResponseData(CONXS, CR, FMAP, ECF) = ECF [owise] . 
endm


mod ENV-TRANSITION-SEMANTICS is
  protecting ENV-TRANSITION-EXEC .

  op execEnv  : BehaviorConf ~> BATransResult [format (m! o)] .
  eq execEnv(BCF:BehaviorConf) = execEnvTrans(BCF:BehaviorConf) .
endm

