

--- An environment component for concrete semantics
mod ENV-WITH-TIMER-COMPONENT is
  including ENV-COMPONENT .
  including LIST{ComponentRef} .

  ---(
  class EnvWithTimer | timer .
  subclass EnvWithTimer < Env .
  ---)
  sort EnvWithTimer .
  subsort EnvWithTimer < Env .
  op EnvWithTimer : -> EnvWithTimer [ctor] .
  op timer`:_ : Rat -> Attribute [ctor gather(&)] .
  op samplingIDs`:_ : List{ComponentRef} -> Attribute [ctor gather(&)] .
  op responseIDs`:_ : List{ComponentRef} -> Attribute [ctor gather(&)] .
endm


fmod SAMPLER is
  protecting RAT .
  protecting RANDOM .
  including LIST{ComponentRef} .

  vars LT UT : Rat .
  var N : Nat .
  var CR : ComponentRef .
  vars CRL CRL' : List{ComponentRef} .

  op rand : Rat Rat Nat ~> Rat .
  eq rand(LT,UT,N) = LT + (UT + - LT) * (random(N) / 4294967295) .

  op randomPick : List{ComponentRef} Nat ~> List{ComponentRef} .
  eq randomPick(CRL, N) = pickNth(CRL, nil, floor((random(N) / 4294967295) * size(CRL))) .

  op pickNth : List{ComponentRef} List{ComponentRef} Nat ~> ComponentRef .
  eq pickNth(CR CRL, CRL', 0)    = CR CRL CRL' .
  eq pickNth(CR CRL, CRL', s(N)) = pickNth(CRL, CR CRL', N) .
endfm



mod ENV-TRANSITION-SEMANTICS is
  including ENV-WITH-TIMER-COMPONENT .
  including ENV-CONTINUOUS-SEMANTICS .
  including BEHAVIOR-TRANSITION-RESULT .
  including ENV-TRANSITION-AUX .
  including SAMPLER .
  protecting DEFAULT-PROPERTIES .
  protecting HYBRID-SYNCHAADL-PROPERTIES .
  protecting CHECKSAT .

  vars L L' : Location .
  vars T T' LT UT : Rat .
  vars SIT SIT' : Set{InterTiming} .
  vars CRL CRL' : List{ComponentRef} .
  var JUMPS : Set{EnvJump} .
  var FLOW : FlowItem .
  var FLOWS : Set{EnvFlow} .
  var FMAP : FeatureMap .
  var D : DataContent .
  vars E E' : Exp .
  vars B B' B'' : BoolExp .
  vars SK PER : RealExp .
  var CONXS : Set{Connection} .
  vars DATA DATA' : DataValuation .
  var CI : ComponentId .
  var PI : FeatureId .
  var PRS : Set{FeatureRef} .
  var CR : ComponentRef .
  vars ECF ECF' ECF'' : BehaviorConf .
  vars PORTS PORTS' : Configuration .
  var PROPS : PropertyAssociation .
  var ATTS : AttributeSet .
  var N : Nat .

  op time : Rat -> BehaviorConfItem [ctor format (b! o)] .
  op sIds : List{ComponentRef} -> BehaviorConfItem [ctor format (b! o)] .
  op rIds : List{ComponentRef} -> BehaviorConfItem [ctor format (b! o)] .
  op randC : Nat -> BehaviorConfItem [ctor format (b! o)] .


  sort Tuple{Time,BATransResult,Nat} .
  op _|_|_ : Rat BATransResult Nat -> Tuple{Time,BATransResult,Nat} [ctor] .

--- sample step; assume no symbolic variable
  op execSample : BehaviorConf ~> Tuple{Time,BATransResult,Nat} [format (m! o)] .
  op execSample : BehaviorConf ComponentRef Rat ~> Tuple{Time,BATransResult,Nat} [format (m! o)] .

  --- use frewrite for fair simulation of choosing CR
 ceq [env-sample]: 
     execSample(time(T) prop(PROPS) sampling((CR : (LT,UT), SIT)) sIds(CRL) randC(N) ECF) 
   = execSample(time(T) prop(PROPS) sampling(SIT) sIds(CRL') randC(s s N) ECF, CR, T') 
  if PER # B := eval(p[TimingProperties::Period], prop(PROPS))
  /\ SK # B' := eval(p[HybridSynchAADL::MaxClockDeviation], prop(PROPS)) 
  /\ check-true(B and B')
  /\ CR CRL' := randomPick(CRL, N) 
  /\ T' := rand(max(LT,T), min(UT + 2 * rval(SK), rval(PER)), s N) .

 ceq execSample(time(T)  mode(L) flows((L FLOW) ; FLOWS) envcon(CONXS,PORTS) ECF, CR, T')
   = execSample(time(T') mode(L) flows((L FLOW) ; FLOWS) envcon(CONXS,PORTS) 
                updateEnvFeature(CONXS, PORTS, CR, updateEnvData(DATA,ECF))) 
  if DATA := execFlow(FLOW, real(T' + - T), ECF) .

  eq execSample(time(T) mode(L) sampling(empty) randC(N) ECF) 
   = T | transResult(L,ECF) | N .
    
  sort Tuple{BATransResult,Nat} .
  op _|_ : BATransResult Nat -> Tuple{BATransResult,Nat} [ctor] .

--- response step (sampling should be already empty)
  op execResp : BehaviorConf ~> Tuple{BATransResult,Nat} [format (m! o)] .
  op execResp : BehaviorConf ComponentRef Rat ~> Tuple{BATransResult,Nat} [format (m! o)] .

 ceq [env-resp]: 
     execResp(time(T) prop(PROPS) response((CR : (LT,UT), SIT)) rIds(CRL) randC(N) ECF)
   = execResp(time(T) prop(PROPS) response(SIT) rIds(CRL') randC(s s N) ECF, CR, T') 
  if CR CRL' := randomPick(CRL, N) 
  /\ PER # B := eval(p[TimingProperties::Period], prop(PROPS))
  /\ SK # B' := eval(p[HybridSynchAADL::MaxClockDeviation], prop(PROPS)) 
  /\ check-true(B and B')
  /\ T' := rand(max(LT,T), min(UT + 2 * rval(SK), rval(PER)), s N) .

   --- a command has arrived at port PI from controller CR, together with other data ports connected to CR
   --- NOTE: assume the mode transition is deterministic.
 ceq execResp(time(T)  mode(L)  envcon(CONXS,PORTS) feature(FMAP) ECF, CR, T')
   = execResp(time(T') mode(L') envcon(CONXS,PORTS) feature(FMAP) ECF'')
  if jumps((L -[ PI, PRS ]-> L') ; JUMPS) flows((L FLOW) ; FLOWS) ECF' := ECF 
  /\ check-true(isPortPresent(PI, FMAP)) 
  /\ < PI : C:EnvInPort | target : CR, ATTS > PORTS' := PORTS
  /\ DATA := execFlow(FLOW, real(T' + - T), ECF) 
  /\ ECF'' := updateEnvData(DATA, updateResponseData(CONXS, CR, PORTS, FMAP, ECF)) .

   --- when no command has arrived from controller CR
   --- No mode change, but DATA are updated by data input from CR
 ceq execResp(time(T)  mode(L) envcon(CONXS,PORTS) feature(FMAP) ECF, CR, T')
   = execResp(time(T') mode(L) envcon(CONXS,PORTS) feature(FMAP) ECF'')
  if jumps(JUMPS) flows((L FLOW) ; FLOWS) ECF' := ECF 
  /\ check-true(allPortsAbsent(L, JUMPS, FMAP, CR, PORTS)) 
  /\ DATA := execFlow(FLOW, real(T' + - T), ECF) 
  /\ ECF'' := updateEnvData(DATA, updateResponseData(CONXS, CR, PORTS, FMAP, ECF)) .

 ceq execResp(time(T) mode(L) prop(PROPS) response(empty) flows((L FLOW) ; FLOWS) randC(N) ECF)
   = transResult(L, prop(PROPS) updateEnvData(DATA,ECF)) | N  
  if PER # B := eval(p[TimingProperties::Period], prop(PROPS))
  /\ check-true(B)
  /\ DATA := execFlow(FLOW, PER + real(- T), ECF) .


  ---  update a conf from DATA at once; no constraint maintained 
  op updateEnvData : DataValuation BehaviorConf ~> BehaviorConf .
 ceq updateEnvData((CR |=> E # B, DATA), data(DATA') ECF)
   = updateEnvData(DATA, data(insert(CR, E # bool(true), DATA')) ECF)
    if check-true(B) .
  eq updateEnvData(empty, ECF) = ECF .

  --- update the data from input ports connected to CR; for absent data ports,
  --- previous values are used (i.e. no change on data components)
  op updateResponseData : Set{Connection} ComponentRef Configuration FeatureMap BehaviorConf ~> BehaviorConf [format (m! o)] .

  ceq updateResponseData((PI =>> CI) ; CONXS, CR, PORTS, FMAP, data(DATA) ECF)
    = updateResponseData(CONXS, CR, PORTS, FMAP, 
                         data(insert(CI, (B ? E : E') # (B or B'), DATA)) ECF)
   if < PI : C:EnvInPort | target : CR, ATTS > PORTS' := PORTS
   /\ E  # B : B'' := FMAP[PI]   --- NOTE: B'' (freshness) is always true
   /\ E' # B' := DATA[CI] 
   /\ check-true(B'' and (B or B')) .

  eq updateResponseData(CONXS, CR, PORTS, FMAP, ECF) = ECF [owise] . 
endm


